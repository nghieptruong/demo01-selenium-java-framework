plugins {
    id 'java'
}

group = 'com.automation'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(['--release', '21'])
}

dependencies {
    implementation("org.seleniumhq.selenium:selenium-java:4.35.0")
    implementation("org.testng:testng:7.11.0")
    implementation("org.apache.logging.log4j:log4j-core:2.25.1")
    implementation("org.apache.logging.log4j:log4j-api:2.25.1")
    implementation("com.aventstack:extentreports:5.1.2")

    implementation("net.datafaker:datafaker:2.3.1")
    implementation("io.rest-assured:rest-assured:5.5.6") {
        exclude group: "org.codehaus.jackson"
    }
    implementation("commons-io:commons-io:2.20.0")
    implementation("org.apache.johnzon:johnzon-core:1.2.21")
    implementation("org.apache.johnzon:johnzon-jsonb:1.2.21")

    implementation("io.rest-assured:json-path:5.5.6")
    implementation("io.rest-assured:json-schema-validator:5.5.6")

    implementation("com.fasterxml.jackson.core:jackson-databind:2.20.0")
    implementation("com.google.code.gson:gson:2.13.2")

    implementation("org.yaml:snakeyaml:2.5")

    compileOnly 'org.projectlombok:lombok:1.18.42' // Use the latest version
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
}

tasks.withType(Test) {
    systemProperty 'env', System.properties['env']

    useTestNG() {
        useDefaultListeners = false // Disable TestNG HTML reports (using ExtentReports instead)
        outputDirectory = file("$projectDir/test-output") // folder chá»©a test report

        def groups = System.getProperty('groups')
        // If the 'groups' property is provided, include only those groups
        if (groups != null) {
            useTestNG() {
                includeGroups groups.split(',') // Supports multiple comma-separated groups
            }
        }
    }

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

tasks.register('e2eTests', Test) {
    useTestNG {
        suites "src/test/resources/e2e.xml"
    }
}

tasks.register('smokeTests', Test) {
    useTestNG {
        suites "src/test/resources/smoke.xml"
    }
}

tasks.register('regressionTests', Test) {
    useTestNG {
        suites "src/test/resources/regression.xml"
    }
}

// JavaDoc generation task - creates HTML documentation from JavaDoc comments
// BEST PRACTICE: Only document framework code (src/main/java) + reusable test helpers
// Excludes:
//   - Test cases (testcases.*) - self-documenting via test names and reports
//
// NOTE: Model classes are included because they're referenced in API method signatures,
// but they are simple data containers (Lombok POJOs) with minimal documentation.
javadoc {
    source = sourceSets.main.allJava

    // Include ONLY reusable test helpers (not test cases)
    source += fileTree(dir: 'src/test/java', include: 'helpers/**/*.java')

    classpath = configurations.compileClasspath + configurations.testCompileClasspath
    destinationDir = file("$projectDir/docs/javadoc")

    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = 'Selenium Automation Framework - API Documentation'
    options.docTitle = 'Selenium Automation Framework - Core API'
    options.header = 'Test Automation Framework API'
    options.footer = 'Framework Documentation - For internal use'

    // Group packages for better organization
    options.group("Page Objects", "pages*")
    options.group("Base Classes", "base*")
    options.group("Test Helpers", "helpers*")
    options.group("Utilities", "utils*")
    options.group("Configuration", "config*")
    options.group("Reporting", "reports*")
    options.group("Data Models", "model*")
    options.group("API Clients", "api*")
    options.group("Driver Management", "drivers*")
    options.group("Listeners", "listeners*")

    // Suppress warnings for missing tags
    options.addStringOption('Xdoclint:none', '-quiet')
}
